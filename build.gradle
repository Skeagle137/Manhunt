import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
}

group = 'net.skeagle'
version = '1.1.5'
description = 'manhunt'
sourceCompatibility = 17
targetCompatibility = 17

String mc = '1.18'

repositories {
    mavenCentral()
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = uri('https://jitpack.io') }
    mavenLocal()
}

dependencies {
    implementation 'net.skeagle:VRNLib:1.4a'
    compileOnly "org.spigotmc:spigot-api:${mc}-R0.1-SNAPSHOT"
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }
}

task updatePlugins {
    doLast {
        copy {
            from 'build/libs'
            into "C:/Users/Skeagle/Documents/VRN Stuff/Dev ${mc}/plugins"
        }
    }
}

task deploy(dependsOn: ["jar", "updatePlugins"]) {
    updatePlugins.mustRunAfter jar
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}