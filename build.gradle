import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
}

group = 'net.skeagle'
version = '1.3.0'
description = 'manhunt'

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

Version mc = new Version("18", "1")
String vrnlib = '2.1'

repositories {
    mavenCentral()
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://jitpack.io' }
    mavenLocal()
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }
}

dependencies {
    implementation "net.skeagle:VRNLib:${vrnlib}"
    compileOnly "org.spigotmc:spigot-api:${mc.getVersion()}-R0.1-SNAPSHOT"
}

task updatePlugins {
    doLast {
        copy {
            from 'build/libs'
            into "C:/Users/Skeagle/Documents/VRN Stuff/Dev ${mc.getMain()}/plugins"
            rename { "${rootProject.name} ${version}.jar" }
        }
    }
}

task deploy(dependsOn: ["jar", "updatePlugins"]) {
    updatePlugins.mustRunAfter jar
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}

class Version {

    private String main
    private String sub

    Version(String main) {
        this(main, null)
    }

    Version(String main, String sub) {
        this.main = "1." + main
        this.sub = sub
    }

    String getVersion() {
        return main + (sub != null ? ".$sub" : "")
    }

    String getMain() {
        return main
    }
}